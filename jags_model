jcode3 <- "
model{ 
for(i in 1:N){ 
 	ipdN[i] ~ dpois(exp(log.lambda[i]))
#Model
log.lambda[i] <- log(offset[i]) + alpha_s_g[obs.st.cm.age.indx[i]] 
+ beta1_s_g[obs.st.cm.age.indx[i]]*cos(2*3.14159*epiyrN[i]/period[obs.st.indx[i]] ) 
+ beta2_s_g[obs.st.cm.age.indx[i]]*sin(2*3.14159*epiyrN[i]/period[obs.st.indx[i]] ) 
+ beta3_s_g[obs.st.cm.age.indx[i]]*t1[i]  #pre-vaccine trend
+ beta4_v_s_g[obs.st.cm.age.indx[i]]*t2[i]  #PCV7-associated change
+ beta5_v_s_g[obs.st.cm.age.indx[i]]*t3[i] #PCV13-associated change
+ disp[i] 

disp[i] ~ dnorm( 0, tau.disp) #dispersion parameter
}

#Loop for each serotype/age group/comorbid group combo
for(w in 1:Nstcmagecombos) {
#intercept for the st/rg combo depends on intercept for the rg and intercept for st  
 mu_s_g[w] <- alpha + alpha_s[ts.loop.st.index[w]] +alpha_g[ts.loop.cm.age.index[w]] 
 alpha_s_g[w] ~ dnorm(mu_s_g[w],  tau.alpha_s_g) 
#Harmonic components
 beta1_s_g[w] ~ dnorm(beta1_s[ts.loop.st.index[w]], tau.beta1_s_g) 
 beta2_s_g[w] ~ dnorm(beta2_s[ts.loop.st.index[w]], tau.beta2_s_g) 
#Secular trend
 beta3_s_g[w] ~ dnorm(beta3_s[ts.loop.st.index[w]], tau.beta3_s_g) 
#Vaccine-associated Slope; centered around the estimate for the serotype 
 beta4_v_s_g[w] ~ dnorm(beta4_s[ts.loop.st.index[w]] , tau.beta4_v_s_g)  
 beta5_v_s_g[w] ~ dnorm(beta5_s[ts.loop.st.index[w]] , tau.beta5_v_s_g) 
}

#Serotype-level loop
for(z in 1:Nsts) {
alpha_s [z]~  dnorm(0, tau.alpha_s) 
beta1_s[z]~dnorm(0,tau.beta1_s)
beta2_s[z]~dnorm(0,tau.beta2_s)
beta3_s[z]~dnorm(0,tau.beta3_s)
beta4_s[z]~dnorm(beta4_v[st.loop.pcv.index[z]],tau.beta4_s)
beta5_s[z]~dnorm(beta5_v[st.loop.pcv.index[z]],tau.beta5_s)
period[z]~ dnorm(6.5,1e-5)T(3,10) 
}

#Loop for risk groups, with nested loop for risk group/VT group combos
for(xx in 1:n.risk){
alpha_g[xx] ~  dnorm(0, 1e-5)
}
#Loop for PCV7, PCV13 and NVTs
for(x in 1:3){  
beta4_v[x]~dnorm(0, 1e-5)
beta5_v[x]~dnorm(0, 1e-5)
}
########################################################
###Hyperpriors for observation-level model 
alpha~dnorm(0,1e-5)
sd.disp~dunif(0,100)
tau.disp <-1/sd.disp^2
#hyperpriors for time-series-level loop
sd.beta4_v_s_g ~dunif(0,100)
tau.beta4_v_s_g <-1/ sd.beta4_v_s_g^2
sd.beta5_v_s_g ~dunif(0,100)
tau.beta5_v_s_g <-1/ sd.beta5_v_s_g^2

#Hyperpriors for serotype/risk group loop
sd.beta1_s_g~dunif(0,100)
tau.beta1_s_g<-1/sd.beta1_s_g^2  
sd.beta2_s_g~dunif(0,100)
tau.beta2_s_g<-1/sd.beta2_s_g^2
sd.beta3_s_g~dunif(0,100)
tau.beta3_s_g<-1/sd.beta3_s_g^2
sd.alpha_s_g ~dunif(0,100)
tau.alpha_s_g<-1/sd.alpha_s_g^2

#Hyperpriors for risk group loop
sd.alpha_g~dunif(0,100)
tau.alpha_g<-1/sd.alpha_g^2

###Hyperpriors for ST-level loop loops
tau.ave.period<-1/sd.ave.period^2
sd.beta1_s~dunif(0,100)
tau.beta1_s<-1/sd.beta1_s^2
sd.beta2_s~dunif(0,100)
tau.beta2_s<-1/sd.beta2_s^2
sd.beta3_s~dunif(0,100)
tau.beta3_s<-1/sd.beta3_s^2
sd.b4_s~dunif(0,100)
tau.beta4_s<-1/sd.b4_s^2
sd.b5_s~dunif(0,100)
tau.beta5_s<-1/sd.b5_s^2
sd.alpha_s~dunif(0,100)
tau.alpha_s<-1/sd.alpha_s^2
####################################################################
}
"
